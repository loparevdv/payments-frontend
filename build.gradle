group 'org.jetbrains'
version '1.0-SNAPSHOT'


buildscript {
    ext.kotlin_version = "1.3.31"
    ext.serialization_version = "0.10.0"
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"

    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlinx-serialization'


compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/jsbuild/payments.js"
}

sourceSets {
    main.kotlin.srcDirs += "src"
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
}


task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/jsbuild/libs"

    dependsOn classes
}

assemble.dependsOn assembleWeb